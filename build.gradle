buildscript {
    repositories {
        mavenLocal()
        maven { url 'https://maven.aliyun.com/repository/public' }
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }
}

plugins {
    id 'groovy'
    id "java"
    id "java-gradle-plugin"
    id "maven-publish"
    id "signing"
    id "maven"
    id "com.gradle.plugin-publish" version "0.12.0"
}

group 'com.github.shalousun'
version '2.1.8-SNAPSHOT'

java.sourceCompatibility = JavaVersion.VERSION_1_8
java.targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenLocal()
    maven {
        url 'http://localhost:8081/nexus/content/groups/public/'
        allowInsecureProtocol true
    }
    maven{ url 'https://maven.aliyun.com/repository/public'}
    mavenCentral()
}

dependencies {
    implementation 'com.github.shalousun:smart-doc:2.1.8-SNAPSHOT'
    testImplementation group: 'junit', name: 'junit', version: '4.13.1'
}
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

java {
    withSourcesJar()
    withJavadocJar()
}

signing {
    sign configurations.archives
}

gradlePlugin {
    plugins {
        greetingsPlugin {
            id = 'com.github.shalousun.smart-doc'
            implementationClass = 'com.smartdoc.gradle.plugin.SmartDocPlugin'
        }
    }
}
// publish to https://plugins.gradle.org/
pluginBundle {
    website = 'https://github.com/smart-doc-group/smart-doc-gradle-plugin'
    vcsUrl = 'https://github.com/smart-doc-group/smart-doc-gradle-plugin'
    description = 'smart-doc gradle plugin'
    tags = ['smart-doc']
    plugins {
        greetingsPlugin {
            // id is captured from java-gradle-plugin configuration
            displayName = 'smart-doc gradle plugin'
        }
    }
}

// publish to local
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "http://localhost:8081/nexus/content/repositories/releases/"
            allowInsecureProtocol true

            credentials {
                username 'admin'
                password 'admin123'
            }
        }
    }
}
// Build, sign, and upload
uploadArchives {
    repositories {
        mavenDeployer {
            // Sign POM
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }

            // Destination
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            // Add required metadata to POM
            pom.project {
                name 'smart-doc-gradle-plugin'
                packaging 'jar'
                description 'smart-doc gradle plugin.'
                url 'https://github.com/smart-doc-group/smart-doc-gradle-plugin'
                scm {
                    connection 'scm:git:git@github.com:smart-doc-group/smart-doc-gradle-plugin.git'
                    developerConnection 'scm:git@github.com:smart-doc-group/smart-doc-gradle-plugin.git'
                    url 'https://github.com/smart-doc-group/smart-doc-gradle-plugin'
                }
                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'shalousun'
                        name 'shalousun'
                        email '836575280@qq.com'
                        organization 'smart-doc-group'
                        organizationUrl 'https://github.com/smart-doc-group'
                    }
                }
            }
        }
    }
}